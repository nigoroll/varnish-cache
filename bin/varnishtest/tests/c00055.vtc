varnishtest "test caching of req.body"

server s1 {
	rxreq
	expect req.bodylen == 3
	txresp -hdr "Connection: close" -hdr "Foo: BAR" -body "1234"
	accept
	rxreq
	expect req.bodylen == 3
	txresp -hdr "Foo: Foo" -body "56"
} -start

varnish v1 -vcl+backend {
	import std;

	sub vcl_recv {
		if (std.cache_req_body(std.bytes(req.http.cache, 1KB))) {
			set req.http.stored = true;
		} else {
			set req.http.stored = false;
		}
		return (pass);
	}
	sub vcl_deliver {
		if (resp.http.foo == "BAR") {
			return (restart);
		}
		set resp.http.stored = req.http.stored;
	}
} -start

# check log for the aborted POST
logexpect l1 -v v1 {
	expect * 1006	Begin
	expect * =	FetchError	"^straight insufficient bytes"
} -start

varnish v1 -cliok "param.set debug +syncvsl"

client c1 {
	txreq -req "POST" -body "FOO"
	rxresp
	expect resp.http.Foo == "Foo"
	expect resp.bodylen == 2
	expect resp.http.stored == true
} -run

delay .1

client c2 {
	txreq -req POST -nolen -hdr "Content-Length: 52"
	delay .3
	expect resp.http.stored == <undef>
} -run

delay .1

server s1 {
	rxreq
	txresp
} -start

client c3 {
	txreq -url "/is_varnish_still_running"
	rxresp
	expect resp.status == 200
	expect resp.http.stored == true
} -run

client c4 {
	txreq -req POST -nolen -hdr "Content-Length: 1025"
	expect_close
	expect resp.http.stored == <undef>
} -run

server s1 {
	rxreq
	expect req.body == chunked_body
	txresp
} -start

client c5 {
	txreq -req POST -nolen -hdr "Transfer-Encoding: chunked"
	chunked chunked
	chunked _
	chunked body
	chunkedlen 0
	rxresp
	expect resp.http.stored == true
} -run

server s1 {
	rxreq
	expect req.bodylen == 0
	txresp
} -start

client c6 {
	txreq -req POST -nolen -hdr "Transfer-Encoding: chunked"
	chunkedlen 0
	rxresp
	expect resp.http.stored == true
} -run

server s1 {
	rxreq
	expect req.bodylen == 0
	txresp
} -start

client c7 {
	txreq -req POST -nolen -hdr "cache: 0B" -hdr "Transfer-Encoding: chunked"
	chunkedlen 0
	rxresp
	expect resp.http.stored == false
} -run

varnish v1 -stop
logexpect l1 -wait
