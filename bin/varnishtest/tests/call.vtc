varnishtest "VCL_SUB type basics"

varnish v1 -vcl {
	import debug;
	backend dummy None;

	sub foo {
		set resp.http.it = "works";
	}
	sub indirect {
		debug.call(foo);
	}
	sub direct {
		call foo;
	}
	sub recursive {
		debug.call(recursive);
	}

	sub vcl_recv {
		if (req.url == "/wrong") {
			debug.call(foo);
		}
		if (req.url == "/recursive") {
			debug.call(recursive);
		}
		return (synth(200));
	}
	sub vcl_synth {
		if (req.url == "/foo") {
			debug.call(foo);
		} else if (req.url == "/direct") {
			debug.call(direct);
		} else if (req.url == "/indirect") {
			debug.call(indirect);
		}
		return (deliver);
	}
} -start

client c1 {
	txreq -url "/foo"
	rxresp
	expect resp.http.it == "works"

	txreq -url "/direct"
	rxresp
	expect resp.http.it == "works"

	txreq -url "/indirect"
	rxresp
	expect resp.http.it == "works"

	txreq -url "/wrong"
	rxresp
	expect resp.status == 503
} -run
client c1 {
	txreq -url "/recursive"
	rxresp
	expect resp.status == 503
} -run
