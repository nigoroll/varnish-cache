varnishtest "VCL_SUB type basics"

varnish v1 -vcl {
	import std;
	import debug;

	backend dummy None;

	sub impossible {
		set req.http.impossible = beresp.reason;
	}
	sub foo {
		set resp.http.it = "works";
	}
	sub indirect {
		debug.call(foo);
	}
	sub direct {
		call foo;
	}
	sub recursive {
		debug.call(recursive);
	}
	sub rollback {
		std.rollback(req);
	}

	sub vcl_recv {
		if (req.url == "/impossible") {
			debug.call(impossible);
		}
		if (req.url == "/wrong") {
			debug.call(foo);
		}
		if (req.url == "/recursive") {
			debug.call(recursive);
		}
		return (synth(200));
	}
	sub vcl_synth {
		if (req.url == "/foo") {
			debug.call(foo);
		} else if (req.url == "/direct") {
			debug.call(direct);
		} else if (req.url == "/indirect") {
			debug.call(indirect);
		} else if (req.url == "/rollback") {
			debug.call(rollback);
		} else if (req.url == "/callthenrollback") {
			debug.call(foo);
			call rollback;
			if (! resp.http.it) {
				set resp.http.rolledback = true;
			}
			debug.call(foo);
		}
		return (deliver);
	}
} -start

client c1 {
	txreq -url "/foo"
	rxresp
	expect resp.http.it == "works"

	txreq -url "/direct"
	rxresp
	expect resp.http.it == "works"

	txreq -url "/indirect"
	rxresp
	expect resp.http.it == "works"

	txreq -url "/callthenrollback"
	rxresp
	expect resp.http.rolledback == "true"
	expect resp.http.it == "works"

	txreq -url "/wrong"
	rxresp
	expect resp.status == 503
} -run
client c1 {
	txreq -url "/recursive"
	rxresp
	expect resp.status == 503
} -run
client c1 {
	txreq -url "/impossible"
	rxresp
	expect resp.status == 503
} -run
client c1 {
	txreq -url "/rollback"
	# no 503 synth response from vcl_synth for VRT_fail()
	expect_close
} -run
