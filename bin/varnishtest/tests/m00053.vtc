varnishtest "VCL_SUB type basics"

varnish v1 -vcl {
	import std;
	import debug;

	backend dummy None;

	sub impossible {
		set req.http.impossible = beresp.reason;
	}
	sub foo {
		set resp.http.it = "works";
	}
	sub indirect {
		debug.call(foo);
	}
	sub direct {
		call foo;
	}
	sub recursive {
		debug.call(recursive);
	}
	sub recursive_indirect {
		debug.call(recursive_indirect);
	}
	sub rollback {
		std.rollback(req);
	}

	sub vcl_recv {
		if (req.url == "/impossible") {
			debug.call(impossible);
		}
		if (req.url == "/wrong") {
			debug.call(foo);
		}
		if (req.url == "/recursive") {
			debug.call(recursive);
		}
		if (req.url == "/recursive_indirect") {
			call recursive_indirect;
		}
		return (synth(200));
	}
	sub vcl_synth {
		if (req.url == "/foo") {
			debug.call(foo);
		} else if (req.url == "/direct") {
			debug.call(direct);
		} else if (req.url == "/indirect") {
			debug.call(indirect);
		} else if (req.url == "/rollback") {
			debug.call(rollback);
		} else if (req.url == "/callthenrollback") {
			debug.call(foo);
			call rollback;
			if (! resp.http.it) {
				set resp.http.rolledback = true;
			}
			debug.call(foo);
		}
		return (deliver);
	}
} -start

client c1 {
	txreq -url "/foo"
	rxresp
	expect resp.status == 200
	expect resp.http.it == "works"

	txreq -url "/direct"
	rxresp
	expect resp.status == 200
	expect resp.http.it == "works"

	txreq -url "/indirect"
	rxresp
	expect resp.status == 200
	expect resp.http.it == "works"

	txreq -url "/callthenrollback"
	rxresp
	expect resp.status == 200
	expect resp.http.rolledback == "true"
	expect resp.http.it == "works"

	txreq -url "/wrong"
	rxresp
	expect resp.status == 503
} -start
client c2 {
	txreq -url "/recursive"
	rxresp
	expect resp.status == 503
} -start
client c3 {
	txreq -url "/recursive_indirect"
	rxresp
	expect resp.status == 503
} -start
client c4 {
	txreq -url "/impossible"
	rxresp
	expect resp.status == 503
} -start
client c5 {
	txreq -url "/rollback"
	rxresp
	expect resp.status == 200
} -start
client c1 -wait
client c2 -wait
client c3 -wait
client c4 -wait
client c5 -wait
